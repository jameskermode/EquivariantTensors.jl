
using MLDataDevices: AbstractDevice
import LuxCore: AbstractLuxLayer, initialparameters, initialstates

import Adapt
using Adapt: adapt 


struct ETGraph{VECI, TN, TE, TG}
   ii::VECI     # center particle indices / source indices
   jj::VECI     # neighbour particle indices / target indices
   first::VECI   # first[i] = first index of (i, j) pairs in ii, jj
   node_data::TN     # node data 
   edge_data::TE     # edge data 
   graph_data::TG    # graph data
   maxneigs::Int     # maximum number of neighbors per node (for allocations)                      
end

nnodes(X::ETGraph) = length(X.first) - 1
nedges(X::ETGraph) = length(X.ii)
maxneigs(X::ETGraph) = X.maxneigs

function ETGraph(ii::AbstractVector{TI}, jj::AbstractVector{TI}; 
                 node_data = nothing, edge_data = nothing, graph_data = nothing
                 ) where {TI}
   if !issorted(ii) 
      error("i indices must be sorted")
   end

   nnodes = ii[end] 
   nedges = length(ii)

   # recompute the "first" array 
   first = similar(ii, (nnodes + 1,))
   first[1] = 1 
   idx = 1 
   for t = 1:length(ii) 
      if ii[t] > idx 
         while idx < ii[t]
            first[idx + 1] = t
            idx += 1
         end
      end
   end 
   first[end] = nedges + 1

   maxneigs = Int(maximum(first[2:end] .- first[1:end-1]))

   return ETGraph(ii, jj, first, node_data, edge_data, graph_data, maxneigs)
end               

function Adapt.adapt_structure(to, X::ETGraph) 
   ETGraph( adapt(to, X.ii), adapt(to, X.jj), adapt(to, X.first), 
            adapt(to, X.node_data), adapt(to, X.edge_data), 
            adapt(to, X.graph_data), X.maxneigs)
end

function (dev::AbstractDevice)(X::ETGraph) 
   ETGraph(dev(X.ii), dev(X.jj), dev(X.first), 
              dev(X.node_data), dev(X.edge_data), dev(X.graph_data), 
              X.maxneigs)
end

function neighbourhood(X::ETGraph, i::Int)
   # Returns the indices and edge data of the neighbours of node i
   #  (maybe it should also return node data of i and j ~ i??)
   first = X.first[i]
   last = X.first[i+1] - 1
   return X.jj[first:last], X.edge_data[first:last]
end


# ----------------------------------------------- 
# utility functions to work with the ETGraph and embedding it into 
# various formats for further processing

__zero(x) = zero(x) 
__zero(TX::Type{<: NamedTuple}) = DiffNT.__zero(TX)

"""
   reshape_embedding(P, X::ETGraph)

Takes a Nedges x Nfeat matrix and writes it into a 3-dimensional array of
size (maxneigs, nnodes, Nfeat) where each column corresponds to a node.
The "missing" neighbours are filled with zeros.

When P is a Reactant TracedRArray, dispatches to pure Julia implementation.
Otherwise uses KernelAbstractions for GPU compatibility.
"""
function reshape_embedding(P, X::ETGraph)
   if _is_reactant_traced(P)
      return _reactant_reshape_embedding(P, X)
   end
   return _ka_reshape_embedding(P, X)
end

# Pure Julia fallback for Reactant (overridden in ReactantExt)
function _reactant_reshape_embedding(P, X::ETGraph)
   error("Reactant.jl must be loaded for Reactant-compatible reshape_embedding")
end

# KernelAbstractions implementation
function _ka_reshape_embedding(P, X::ETGraph)
   @kernel function _reshape_embedding_kernel!(P3, @Const(P), @Const(first))
      inode, ifeat = @index(Global, NTuple)
      i1 = first[inode]
      i2 = first[inode + 1] - 1
      for t = 1:(i2-i1+1)
         iedge = i1 + t - 1  # edge index
         @inbounds P3[t, inode, ifeat] = P[iedge, ifeat]
      end
      nothing
   end

   # size(P) == #edges x # features
   nedges, nfeatures = size(P)
   P3 = similar(P, (maxneigs(X), nnodes(X), nfeatures))
   fill!(P3, __zero(eltype(P3)))    # TODO : nasty hack, another reason to switch to DecoratedParticles
   backend = KernelAbstractions.get_backend(P3)
   kernel! = _reshape_embedding_kernel!(backend)
   kernel!(P3, P, X.first; ndrange = (nnodes(X), nfeatures))
   KernelAbstractions.synchronize(backend)
   return P3
end

"""
   rev_reshape_embedding(P3, X::ETGraph) -> P

Reverse operation for `reshape_embedding`. P3 is of shape
(maxneigs, nnodes, nfeatures), and this gets written into P which is
of shape (nedges, nfeatures) and then returned.

When P3 is a Reactant TracedRArray, dispatches to pure Julia implementation.
"""
function rev_reshape_embedding(P3, X::ETGraph)
   if _is_reactant_traced(P3)
      return _reactant_rev_reshape_embedding(P3, X)
   end
   return _ka_rev_reshape_embedding(P3, X)
end

# Pure Julia fallback for Reactant (overridden in ReactantExt)
function _reactant_rev_reshape_embedding(P3, X::ETGraph)
   error("Reactant.jl must be loaded for Reactant-compatible rev_reshape_embedding")
end

# KernelAbstractions implementation
function _ka_rev_reshape_embedding(P3, X::ETGraph)
   @kernel function _rev_reshape_embedding_kernel!(P, @Const(P3), @Const(first))
      inode, ifeat = @index(Global, NTuple)
      i1 = first[inode]
      i2 = first[inode + 1] - 1
      for t = 1:(i2-i1+1)
         iedge = i1 + t - 1  # edge index
         @inbounds P[iedge, ifeat] = P3[t, inode, ifeat]
      end
      nothing
   end

   # size(P3) == maxneigs x #nodes x #features
   nedg = nedges(X)
   nfeatures = size(P3, 3)
   P = similar(P3, (nedg, nfeatures))
   fill!(P, zero(eltype(P3)))

   backend = KernelAbstractions.get_backend(P)
   kernel! = _rev_reshape_embedding_kernel!(backend)
   kernel!(P, P3, X.first; ndrange = (nnodes(X), nfeatures))
   KernelAbstractions.synchronize(backend)
   return P
end


function rrule(::typeof(reshape_embedding), ϕ2, X::ETGraph)
   ϕ3 = reshape_embedding(ϕ2, X)

   function _pb_ϕ(∂ϕ3)
      ∂ϕ2 = rev_reshape_embedding(unthunk(∂ϕ3), X)
      return NoTangent(), ∂ϕ2, NoTangent()
   end

   return ϕ3, _pb_ϕ
end
